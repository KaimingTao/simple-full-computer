// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:

    DMux8Way(in=load, sel=address[6..8],
            a=rg0,
            b=rg1,
            c=rg2,
            d=rg3,
            e=rg4,
            f=rg5,
            g=rg6,
            h=rg7
            );
    RAM64(in=in, load=rg0, address=address[0..5], out=or0);
    RAM64(in=in, load=rg1, address=address[0..5], out=or1);
    RAM64(in=in, load=rg2, address=address[0..5], out=or2);
    RAM64(in=in, load=rg3, address=address[0..5], out=or3);
    RAM64(in=in, load=rg4, address=address[0..5], out=or4);
    RAM64(in=in, load=rg5, address=address[0..5], out=or5);
    RAM64(in=in, load=rg6, address=address[0..5], out=or6);
    RAM64(in=in, load=rg7, address=address[0..5], out=or7);

    Mux8Way16(
            a=or0,
            b=or1,
            c=or2,
            d=or3,
            e=or4,
            f=or5,
            g=or6,
            h=or7,
            sel=address[6..8], out=out
            );

}